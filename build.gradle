apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.google.protobuf'
apply plugin: 'git-repo'

buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
		classpath 'com.ryanharter.gradle-git-repo:gradle-plugin:1.0.1'
	}
}

group = 'org.hyperledger.fabric-sdk-java'
version = '1.1.0-SNAPSHOT'

description = """fabric-java-sdk"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

protobuf {
	protoc {
		// download the protobuf compiler
		artifact = 'com.google.protobuf:protoc:3.0.0'
	}
	plugins {
		// define grpc plugin for the protobuf compiler
		grpc {
			artifact = 'io.grpc:protoc-gen-grpc-java:1.5.0'
		}
	}
	generateProtoTasks {
		all()*.plugins {
			// apply the grpc plugin for the protobuf compiler
			grpc {}
		}
	}
}

sourceSets {
	main {
		java {
			// adding the protobuf & grpc generated source explicitly,
			// so that IDEs can find the generated sources.
			srcDirs 'build/generated/source/proto/main/grpc'
			srcDirs 'build/generated/source/proto/main/java'
		}
	}
}

repositories {
	maven { url "http://mvn.sl.cloud9.ibm.com/public" }
	maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
	compile group: 'io.grpc', name: 'grpc-netty', version: '1.5.0'
	compile group: 'io.grpc', name: 'grpc-protobuf', version: '1.5.0'
	compile group: 'io.grpc', name: 'grpc-stub', version: '1.5.0'
	compile group: 'io.netty', name: 'netty-tcnative-boringssl-static', version: '2.0.5.Final'
	compile group: 'io.netty', name: 'netty-codec-http2', version: '4.1.13.Final'
	compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.4.0'
	compile group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.57'
	compile group: 'commons-logging', name: 'commons-logging', version: '1.2'
	compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
	compile group: 'org.apache.commons', name: 'commons-compress', version: '1.14'
	compile group: 'commons-io', name: 'commons-io', version: '2.5'
	compile group: 'log4j', name: 'log4j', version: '1.2.17'
	compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.3'
	compile group: 'org.glassfish', name: 'javax.json', version: '1.1'
	compile group: 'org.yaml', name: 'snakeyaml', version: '1.18'
	testCompile group: 'junit', name: 'junit', version: '4.12'
	testCompile group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.3'
}

test {
	include '**/*Test.*'
}

uploadArchives {
	group = "upload"

	onlyIf {
		!project.name.contains("codegen")
	}
	configuration = configurations.archives
	repositories {
		mavenDeployer {
			repository(url: 'git@github.com:xuanyue202/harmony-mvn.git/releases')
			snapshotRepository(url: 'git@github.com:xuanyue202/harmony-mvn.git/snapshots')
		}
	}
}