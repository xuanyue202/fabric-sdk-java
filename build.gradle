plugins {
  id "com.google.protobuf" version "0.8.2"
}


apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven'

group = 'com.github.xuanyue202'
version = '1.0.5'

description = """fabric-java-sdk"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}


repositories {
        
     maven { url "http://repo.maven.apache.org/maven2" }
}
dependencies {
    compile group: 'io.grpc', name: 'grpc-netty', version:'1.5.0'
    compile group: 'io.grpc', name: 'grpc-protobuf', version:'1.5.0'
    compile group: 'io.grpc', name: 'grpc-stub', version:'1.5.0'
    compile group: 'io.netty', name: 'netty-tcnative-boringssl-static', version:'2.0.5.Final'
    compile group: 'io.netty', name: 'netty-codec-http2', version:'4.1.13.Final'
    compile group: 'com.google.protobuf', name: 'protobuf-java', version:'3.3.1'
    compile group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version:'1.57'
    compile group: 'commons-logging', name: 'commons-logging', version:'1.2'
    compile group: 'commons-cli', name: 'commons-cli', version:'1.4'
    compile group: 'org.apache.commons', name: 'commons-compress', version:'1.14'
    compile group: 'commons-io', name: 'commons-io', version:'2.5'
    compile group: 'log4j', name: 'log4j', version:'1.2.17'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version:'4.5.3'
    compile group: 'org.glassfish', name: 'javax.json', version:'1.1'
    compile group: 'org.yaml', name: 'snakeyaml', version:'1.18'
    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'org.apache.httpcomponents', name: 'httpmime', version:'4.5.3'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from project.tasks.javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
               name rootProject.name
               packaging 'jar'
               description rootProject.description
               url 'https://github.com/xuanyue202/fabric-sdk-java'

               scm {
                   url 'scm:git@github.com:xuanyue202/fabric-sdk-java.git'
                   connection 'scm:git@github.com:xuanyue202/fabric-sdk-java.git'
                   developerConnection 'scm:git@github.com:xuanyue202/fabric-sdk-java.git'
               }

               licenses {
                   license {
                       name 'Apache License, Version 2.'
                       url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                       distribution 'repo'
                   }
               }
 	       developers {
                   developer {
                       id 'fabric'
                       name 'Fabric JAVA SDK Developers'
                   }
               }
           }
        }
    }
}

test {
    include '**/*Test.*'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.2.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.4.0'
        }
    }

    generatedFilesBaseDir = "src"

    generateProtoTasks {
        all()*.plugins {
            grpc {
                outputSubDir = "java"
            }
        }
    }
}
